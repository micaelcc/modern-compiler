# Syntactic Grammar for EASYC


<program>              ::= <statement>*
<statement>            ::= <expr> ";"
                         | <if_statement>
                         | <while_statement>
                         | <for_statement>
<compound_statement>   ::= "{" <statement>* "}"

<expr>                 ::= <decl_expr>
                         | <identifier> <reassign_expr_tail>

<arith_expr>           ::= <term> <arith_expr_tail>?
<arith_expr_tail>      ::= "+" <term> <arith_expr_tail>?
                         | "-" <term> <arith_expr_tail>?
<decl_expr>            ::= "let" <space> <identifier> <decl_expr_assign>?
<decl_expr_assign>     ::= "=" <arith_expr>      
<reassign_expr>        ::= <identifier> "=" <arith_expr>
<reassign_expr_tail>   ::= "=" <arith_expr> | <arith_expr_tail>

<array>		   		   ::= "[" <items_array>? "]"
<items_array>          ::= <atom> <items_array_tail>?
<items_array_tail>	   ::= "," <atom> <items_array_tail>?
<term>                 ::= <factor> <term_tail>*
<term_tail>            ::= "*" <factor>
                         | "/" <factor>
<factor>               ::= <factor_sig>* <pow> 
<factor_sign>          ::= "*" | "-"
<pow>                  ::= <atom> "^" <pow> | <atom>            
<atom>                 ::= <digit>+
                         | <identifier>
                         | <string>
                         | "(" <expr> ")"
                         | <array>

<expr_bool>            ::= <expr_bool_or>
<expr_bool_or>         ::= <expr_bool_and> <expr_bool_or_tail>*
<expr_bool_or_tail>    ::= "||" <expr_bool_and>
<expr_bool_and>        ::= <expr_bool_not> <expr_bool_and_tail>*
<expr_bool_and_tail>   ::= "&&" <expr_bool_not>
<expr_bool_not>        ::= "!" <expr_bool_not>
                         | <expr_bool_rel>
                         | "(" <expr_bool> ")"                            
<expr_bool_rel>        ::= <arith_expr> <expr_bool_rel_tail>?
<expr_bool_rel_tail>   ::= <op_bool> <arith_expr>
                         
<op_bool>              ::= "<"
                         | ">"
                         | "<="
                         | ">="
                         | "!="
                         | "=="

<while_statement>      ::= "while" <statement_structure>
<if_statement>         ::= "if" <statement_structure> <elseif_statement>* <else_statement>?
<elseif_statement>     ::= "elseif" <statement_structure>
<else_statement>       ::= "else" <compound_statement>
<statement_structure>  ::= "(" <expr_bool> ")" <compound_statement>
<for_statement>        ::= "for" "(" <assign_expr_list>? ";" <expr_bool>? ";" <assign_expr_list>? ")"
                           <compound_statement>
<assign_expr_list>     ::= <reassign_expr> <assign_expr_tail>?
<assign_expr_tail>     ::= "," <reassign_expr> <assign_expr_tail>?

<identifier>           ::= <array_item> | <letter_or_digit>+
<string>               ::= <quote> <string_items>+ <quote>
<string_items>         ::= <letter> 
                         | <digit>
                         | <space>
                         
<array_item>	       ::= <identifier> "[" <arith_expr> "]"
<letter>               ::= [a-z]
                         | [A-Z]
                         | "_"
<letter_or_digit>      ::= <letter>
                         | <digit>
<digit>                ::= [0-9]
<space>                ::= " "
<new_line>             ::= "\n"
<quote>                ::= "\""
